apply plugin: 'com.android.application'

if (file('../team/debugConfig.gradle').exists()) {
    apply from: '../team/debugConfig.gradle'
}

if (file('../team/releaseConfig.gradle').exists()) {
    apply from: '../team/releaseConfig.gradle'
}

android {
    signingConfigs {
        if (project.hasProperty('debugConfig')) {
            debug {
                storeFile file(project.debugConfig.store_file)
            }
        }

        if (project.hasProperty('releaseConfig')) {
            release {
                storeFile file(project.releaseConfig.store_file)
                storePassword project.releaseConfig.store_password
                keyAlias project.releaseConfig.key_alias
                keyPassword project.releaseConfig.key_password
            }
        }
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId "com.bruno.android"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            resValue "string", "app_version", "${defaultConfig.versionName}.debug"

            if (project.hasProperty('debugConfig')) {
                resValue "string", "google_api_key", project.debugConfig.google_api_key
                resValue "string", "spotify_client_id", project.debugConfig.spotify_client_id
                resValue "string", "spotify_client_secret", project.debugConfig.spotify_client_secret
                resValue "string", "spotify_redirect_uri", project.debugConfig.spotify_redirect_uri
            }
            else {
                resValue "string", "google_api_key", ""
                resValue "string", "spotify_client_id", ""
                resValue "string", "spotify_client_secret", ""
                resValue "string", "spotify_redirect_uri", ""
            }
        }

        if (project.hasProperty('releaseConfig')) {
            release {
                resValue "string", "app_version", "${defaultConfig.versionName}"
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

                resValue "string", "google_api_key", project.releaseConfig.google_api_key
                resValue "string", "spotify_client_id", project.releaseConfig.spotify_client_id
                resValue "string", "spotify_client_secret", project.releaseConfig.spotify_client_secret
                resValue "string", "spotify_redirect_uri", project.releaseConfig.spotify_redirect_uri

                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:2.0.3'
    implementation 'com.android.volley:dc-volley:1.1.0'
    implementation 'androidx.room:room-runtime:2.2.5'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation project(':dependencies:spotify-app-remote')
    implementation project(':dependencies:spotify-auth')
    implementation "com.google.code.gson:gson:2.8.6"
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    implementation "androidx.navigation:navigation-fragment:2.3.0"
    implementation "androidx.navigation:navigation-ui:2.3.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-rc01"
    implementation "androidx.preference:preference:1.1.1"
}
